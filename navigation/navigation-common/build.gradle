/*
 * Copyright (C) 2016 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.PlatformIdentifier
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

androidXMultiplatform {
    android()
    jvm()

    defaultPlatform(PlatformIdentifier.JVM)

    sourceSets {
        androidAndCommonMain {
            dependencies {
                api(libs.kotlinStdlib)
                api("androidx.annotation:annotation:1.1.0")
                implementation("androidx.collection:collection-ktx:1.1.0")
            }
        }

        commmonMain {
            dependsOn(androidAndCommonMain)
            kotlin {
                source(project.configure(project.objects.sourceDirectorySet("commonMain included Kotlin source", "commonMain included Kotlin source")) {
                  srcDir "main/java"
                })
            }
        }

        androidMain {
            dependsOn(androidAndCommonMain)
            dependencies {
                api(libs.kotlinStdlib)
                api("androidx.lifecycle:lifecycle-common:2.6.1")
                api("androidx.lifecycle:lifecycle-runtime-ktx:2.6.1")
                api("androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1")
                api("androidx.savedstate:savedstate-ktx:1.2.1")
                api("androidx.lifecycle:lifecycle-viewmodel-savedstate:2.6.1")
                implementation("androidx.core:core-ktx:1.1.0")
                implementation("androidx.profileinstaller:profileinstaller:1.3.0")
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        test {
            dependsOn(androidMain)
            dependencies {
                implementation(project(":navigation:navigation-testing"))
                implementation("androidx.arch.core:core-testing:2.2.0")
                implementation(libs.junit)
                implementation(libs.mockitoCore4)
                implementation(libs.truth)
                implementation(libs.kotlinStdlib)
                implementation(libs.kotlinCoroutinesTest)
            }
        }

        androidTest {
            dependsOn(androidMain)
            dependencies {
                implementation(libs.kotlinTestJunit)
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.truth)
                implementation(libs.mockitoCore.get().toString(), excludes.bytebuddy)
                implementation(libs.dexmakerMockito.get().toString(), excludes.bytebuddy)
                implementation(libs.kotlinStdlib)
                implementation(libs.multidex)
            }
        }
    }
}

android {
    buildTypes.all {
        consumerProguardFiles "proguard-rules.pro"
    }
    defaultConfig {
        multiDexEnabled true
    }
    namespace "androidx.navigation.common"
}

dependencies {
    lintPublish(project(':navigation:navigation-common-lint'))
}

androidx {
    name = "Navigation Common"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2017"
    description = "Android Navigation-Common"
}
