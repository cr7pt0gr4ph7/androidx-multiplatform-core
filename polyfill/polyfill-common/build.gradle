/*
 * Copyright 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.PlatformIdentifier
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

androidXMultiplatform {
    android()
    jvm()

    defaultPlatform(PlatformIdentifier.JVM)

    sourceSets {
        // Android is compiled separately, and does not reference the nonAndroid common sources.
        // The classed defined in nonAndroidMain will therefore be resolved from the
        // Android SDK instead (which is the whole purpose of this package).

        nonAndroidMain {
            dependsOn(commonMain)
            kotlin {
                srcDir "src/nonAndroidMain/kotlin"
            }
            dependencies {
                api(libs.kotlinStdlib)
                api("androidx.annotation:annotation:1.1.0")
                api(projectOrArtifact(":collection:collection"))
            }
        }

        jvmMain {
            dependsOn(nonAndroidMain)
        }
    }
}

android {
    namespace "androidx.polyfill"
}

androidx {
    name = "Android Polyfills"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2023"
    description = "Polyfills for non-Android platforms"
}


var p = project

task compilations {
    doLast {
        println("!!!")
        println("== Source sets ==")
        p.kotlin.sourceSets.all { sourceSet ->
            println(sourceSet)
            println("  source directories:")
            sourceSet.kotlin.srcDirs.each { srcDir ->
                println("    " + srcDir)
            }
            println("  depends on:")
            sourceSet.dependsOn.each { otherSourceSet ->
                println("    " + otherSourceSet)
            }
        }
        println("== Targets ==")
        project.kotlin.targets.all { target ->
            println(target)
            target.compilations.all { compilation ->
                println("  " + compilation)
                println("    " + compilation.defaultSourceSet)
                println("      " + compilation.defaultSourceSet.kotlin)
                println("        source directories:")
                compilation.defaultSourceSet.kotlin.srcDirs.each { srcDir ->
                    println("          " + srcDir)
                }
                println("        depends on:")
                compilation.defaultSourceSet.dependsOn.each { otherSourceSet ->
                    println("          " + otherSourceSet)
                }
                println("        dependency files:")
                try {
                    compilation.compileDependencyFiles.each { dependencyFile ->
                        println("          " + dependencyFile)
                    }
                } catch(Exception e) {
                    println("              Resolution failed.")
                }
            }
        }
        println("???")
    }
}
